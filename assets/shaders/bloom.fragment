#version 300 es
precision mediump float;

out vec4 FragColor;
in vec2 TexCoords;

uniform sampler2D image;
uniform bool horizontal;
float weight[5] = float[] (0.227027, 0.1945946, 0.1216216, 0.054054, 0.016216);

void main() {
    vec2 tex_offset = 1.0 / vec2(textureSize(image, 0)); // gets size of single texel
    vec3 result = texture(image, TexCoords).rgb * weight[0]; // current fragment's contribution
    if(horizontal) {
        for(float i = 1.0; i < 5.0; i += 1.0) {
            float w = weight[int(i)];
            result += texture(image, TexCoords + vec2(tex_offset.x * i, 0.0)).rgb * w;
            result += texture(image, TexCoords - vec2(tex_offset.x * i, 0.0)).rgb * w;
        }
    }
    else {
        for(float i = 1.0; i < 5.0; i += 1.0) {
            float w = weight[int(i)];
            result += texture(image, TexCoords + vec2(0.0, tex_offset.y * i)).rgb * w;
            result += texture(image, TexCoords - vec2(0.0, tex_offset.y * i)).rgb * w;
        }
    }
    FragColor = vec4(result, 1.0);
}